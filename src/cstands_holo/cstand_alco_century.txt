@name ptgamma/cstands_holo/cstand_alco_century
@inputs MU_Master Pod:entity ControlStand:wirelink Number:string Horn1 Quill1 Horn2 Quill2 ForceSand BrakeCutIn EmBrake BellToggle LightToggle MiscToggle HEP_Notch Active RemoteControl UsingRearSeat HumpMultiplier EC_Clamp
@outputs CtlTable:table Bell Light Misc CONSTANT RemoteMode SelectorUp SelectorDown IncreaseDynamics DecreaseDynamics
@persist NotchedThrottle NumThrottleNotches HasDynamics NotchedDynamics TransitionMode TransitionControlType AirBrakeMode LocoBrakeMode InitialReduction Lock_26L Bleeding CTL_Version:string
@persist [Lever_ThrottleOn Lever_ThrottleOff Lever_ThrottleNotchUp Lever_ThrottleNotchDown Lever_ThrottleMax]:string
@persist [Lever_ReverserF Lever_ReverserN Lever_ReverserB]:string
@persist [Lever_ABEmergency Lever_ABEmergencyRecover Lever_ABSuppressionOn Lever_ABSuppressionOff Lever_ABMove Lever_ABNotch Lever_ABRelease]:string
@persist [AirBrakeApply AirBrakeApplyInitial AirBrakeRelease]:string
@persist [LocoBrakeFull LocoBrakeReleaseHard LocoBrakeReleaseSoft]:string
@persist NQ Notch DNQ DynaNotch Dynamics SelectorQ RevQ ABQ HissQ LBQ LHissQ
@persist ControlPrintOrder:array DriverQ Routing:table DriverOld:entity Controls:table Shift:string
@persist ReverserF ReverserB IncreaseThrottle DecreaseThrottle IncreaseDynamics DecreaseDynamics KeyDynUp KeyDynDn IncreaseTrainBrake DecreaseTrainBrake IncreaseLocoBrake DecreaseLocoBrake SelectorUp SelectorDown
@persist HandBrake EmergencyBrake Sander
@persist SetReverser SetThrottle SetDynamics SetSelector SetBrakeNotch SetEqualRes SetLocoBrakeNotch SetLocoBrakePressure SetBleeding FeedPressure FullReleaseCock WarningHissQ
@persist HornType C1P:vector C1A:angle CordAnchor1:vector CordAnchor2:vector CordExtendLength DongleHeight PullLength DownRatio
@persist CS:entity LeverBased
@persist Thetas:table LocoBrakePitch
@persist ReverserAlphaQ
@trigger 
if(duped()){
    reset()
}
if(first()){
    #FOR USE WITH RLC PT 2.0.66 OR OTHER COMPATIBLE VERSIONS
    CtlTable["CS_Version",string] = "0.1.0"
    
    #General Setup Instructions:
    #You will need one of these with RLC PT2. Spawn it somewhere and get wiring! See the RLC PT2 Operator's Manual for more instructions.
    #Don't neglect to check the config!
    #The "HEP_Notch" input is only necessary if the locomotive has HEP.
    #The "Active" and "RemoteControl" inputs are only necessary if the locomotive is equipped with Remote Control capability; "Active" goes to the Pod Controller, "RemoteControl" to a separate toggled button.
    #The "UsingRearSeat" input  is only necessary if the locomotive features Dual Cabs or Dual Control Stands.
    
    #Config
    
    #Pick a Control Scheme File! You can find them in "rlctitanium/src/controls/" !
    #include "rlctitanium/src/controls/Standard"
    
    AirBrakeMode = 2
    #0 - Simple Self-Lapping (Easy)
    #1 - Simple Manual Lapping (Medium)
    #2 - 26L Self-Lapping (Medium-Hard)
    #3 - 24RL Manual Lapping (Hard)
    #4 - No. 6 (6SL/6BL/6DS/6ET) Manual Lapping (Hard)
    #24RL, No. 6, and 26L are the hardest, but also the most rewarding!
    
    LocoBrakeMode = 0 #0 for Self Lapping, 1 for Manual Lapping
    
    InitialReduction = 6 #If using 24RL or 26L brake modes, the train brake will automatically drop this much pressure in First Service or Initial Reduction positions.
    FeedPressure = 90 #Feed Valve pressure in PSI. 70 was the standard until the 1950s, 90 is the standard for freight. 110 is the passenger standard, 120 is sometimes used by mountain railroads.
    
    NotchedThrottle = 1 #0 for Continuous Throttle, 1 for Notched Throttle
    NumThrottleNotches = 8 #Number of Throttle Notches, not counting idle (If Used).
    
    HasDynamics = 1 #1 for Has Dynamic Brakes, 0 for Doesn't Have Dynamic Brakes
    
    NotchedDynamics = 0 #0 for Continuous Dynamic Brakes, 1 for Notched Dynamic Brakes
    
    TransitionMode = 1 #0 for No Transition, 1 for Transition
    TransitionControlType = 1 #0 for MANUAL Transition, 1 for Automatic Transition With Selector, 2 for Fully Automatic Transition
    
    HornType = 0 #0 for Lever, 1 for Cord
    
    CordExtendLength = 4 #How far from anchor point is the horn valve at the top?
    DongleHeight = 0.125 #Fraction of total cord length to place Dongle at
    PullLength = 1 #How many units to pull the cord towards you?
    DownRatio = 0.5 #Pull the dongle (PullLength x DownRatio) units down as well
    
    #Sounds (Don't touch unless you know what you're doing!)
    
    Lever_ThrottleOn = "trans_train_sounds/controls/kc92/rs_throttle_idleto1.wav"
    Lever_ThrottleOff = "trans_train_sounds/controls/kc92/rs_throttle_1toidle.wav"
    Lever_ThrottleNotchUp = "trans_train_sounds/controls/kc92/rs_throttle_notchup.wav"
    Lever_ThrottleNotchDown = "trans_train_sounds/controls/kc92/rs_throttle_notchdown.wav"
    Lever_ThrottleMax = "trans_train_sounds/controls/kc92/rs_throttle_notchupmax.wav"
    
    Lever_ReverserF = "trans_train_sounds/controls/kc92/rs_reverser_forward.wav"      
    Lever_ReverserN = "trans_train_sounds/controls/kc92/rs_reverser_neutral.wav"
    Lever_ReverserB = "trans_train_sounds/controls/kc92/rs_reverser_reverse.wav"
    
    Lever_ABEmergency = "trans_train_sounds/controls/aar/auto_lever_emergency.wav"
    Lever_ABEmergencyRecover = "trans_train_sounds/controls/aar/auto_lever_emergency_recover.wav"
    Lever_ABSuppressionOn = "trans_train_sounds/controls/aar/auto_lever_suppression_on.wav"
    Lever_ABSuppressionOff = "trans_train_sounds/controls/aar/auto_lever_suppression_off.wav"
    Lever_ABMove = "trans_train_sounds/controls/aar/auto_lever_move.wav"
    Lever_ABNotch = "trans_train_sounds/controls/aar/auto_lever_initial.wav"
    Lever_ABRelease = "trans_train_sounds/controls/aar/auto_lever_release.wav"
    
    AirBrakeApply = "trans_train_sounds/controls/aar/auto_air_apply_alt.wav"
    AirBrakeApplyInitial = "trans_train_sounds/controls/aar/auto_air_apply_initial_alt.wav"
    AirBrakeRelease = "trans_train_sounds/controls/aar/auto_air_release.wav"
    
    LocoBrakeFull = "trans_train_sounds/controls/aar/indy_full.wav"
    LocoBrakeReleaseSoft = "trans_train_sounds/controls/aar/indy_release_soft.wav"
    LocoBrakeReleaseHard = "trans_train_sounds/controls/aar/indy_release_full.wav"
    
    WarningHiss = "gsgtrainsounds/misc/desktop/horn_air_loop.wav"
    
    function vector linpos(V1:vector, V2:vector, P){
        return V1 + P*(V2-V1)
    }
    
    function normal adc(MinVal, MaxVal, Resolution, Input){
        local Output = 0
        if(inrange(Input,MinVal,MaxVal)){
            Output = round(Resolution*((Input - MinVal)/(MaxVal-MinVal)))
            
        }elseif(Input < MinVal){
            Output = 0
        }elseif(Input > MaxVal){
            Output = Resolution
        }
        
        return Output
    }
    
    function normal dac(MinVal, MaxVal, Resolution, Input){
        local Output = 0
        if(inrange(Input,0,Resolution)){
            Input = round(Input)
            Output = (MaxVal - MinVal)*(Input/Resolution) + MinVal
        }elseif(Input < 0){
            Output = MinVal
        }elseif(Input > Resolution){
            Output = MaxVal
        }
        return Output
    }
    
    function void entity:printProper(Message:string){
        if(This==owner()){
            print(Message)
        }else{
            Pod:printDriver(Message)
        }
    }
    
    function void lever(HoloIndex,Angle,Speed){
        local Current = Thetas[HoloIndex,number]
        if(Current>Angle){
            Current -= Speed
            if(Current<Angle){Current = Angle}
            Thetas[HoloIndex,number] = Current
            holoAng(HoloIndex,CS:toWorld(ang(0,Current,0)))
        }elseif(Current<Angle){
            Current += Speed
            if(Current>Angle){Current = Angle}
            Thetas[HoloIndex,number] = Current
            holoAng(HoloIndex,CS:toWorld(ang(0,Current,0)))
        }
    }
    
    function void lever2(HoloIndex,Angle,Speed){ #Used for Brake Cutout
        local Current = Thetas[HoloIndex,number]
        if(Current>Angle){
            Current -= Speed
            if(Current<Angle){Current = Angle}
            Thetas[HoloIndex,number] = Current
            holoAng(HoloIndex,CS:toWorld(ang(Current,0,90)))
        }elseif(Current<Angle){
            Current += Speed
            if(Current>Angle){Current = Angle}
            Thetas[HoloIndex,number] = Current
            holoAng(HoloIndex,CS:toWorld(ang(Current,0,90)))
        }
    }
    
    function void lever(HoloIndex,Angle,Pitch,Speed){
        
        if(LocoBrakePitch > Pitch){
            LocoBrakePitch -= Speed
            if(LocoBrakePitch < Pitch){LocoBrakePitch = Pitch}
            holoAng(HoloIndex,CS:toWorld(ang(LocoBrakePitch,Angle,0)))
        }elseif(LocoBrakePitch < Pitch){
            LocoBrakePitch += Speed
            if(LocoBrakePitch > Pitch){LocoBrakePitch = Pitch}
            holoAng(HoloIndex,CS:toWorld(ang(LocoBrakePitch,Angle,0)))
        }
    }
    
    ControlPrintOrder = array(
        "ReverserF",
        "ReverserB",
        "IncreaseThrottle",
        "DecreaseThrottle",
        "IncreaseDynamics",
        "DecreaseDynamics",
        "SelectorUp",
        "SelectorDown",
        "IncreaseTrainBrake",
        "DecreaseTrainBrake",
        "IncreaseLocoBrake",
        "DecreaseLocoBrake",
        "EmergencyBrake",
        "HandBrake",
        "Sander",
        "UseButtons"
    )
    
    #Controls Init
    foreach(K,V:string = Controls){
        if(V:find(" +Shift")){
            Routing[K,number] = 1
            local EXP = V:explode(" ")
            Controls[K,string] = EXP[1,string]
        }else{
            Routing[K,number] = 0
        }
    }
    
    
    CtlTable["AirBrakeMode",number] = AirBrakeMode
    CtlTable["InitialReduction",number] = InitialReduction
    CtlTable["NotchedThrottle",number] = NotchedThrottle
    CtlTable["NumThrottleNotches",number] = NumThrottleNotches
    CtlTable["NotchedDynamics",number] = NotchedDynamics
    CtlTable["TransitionMode",number] = TransitionMode
    CtlTable["TransitionControlType",number] = TransitionControlType
    
    if(AirBrakeMode==2){#26L
        SetBrakeNotch = 6
    }elseif(AirBrakeMode>=3){#24RL/6SL
        SetBrakeNotch = 6
        #print("Yarg")   
    }elseif(AirBrakeMode==1){#Simple ML
        SetBrakeNotch = 2
    }else{ #Simple SL
        SetBrakeNotch = 0
    }
    LocoBrakeNotch = 0
    
    CONSTANT = 1
    
    
    #Control Stand Init
    CS = ControlStand:entity()
    
    #Reverser
    holoCreate(4,CS:toWorld(vec(-11,6,11.5)),vec(1),CS:toWorld(ang(0,90,0)),vec(255),"models/flak/part/stands/century/reverser.mdl")
    Thetas[4,number] = 90
    #Throttle
    holoCreate(5,CS:toWorld(vec(-10,-4,18)),vec(1),CS:toWorld(ang(0,118,0)),vec(255),"models/flak/part/stands/century/throttle.mdl")
    Thetas[5,number] = 118
    #Selector
    holoCreate(6,CS:toWorld(vec(-2,3,18)),vec(1),CS:toWorld(ang(0,0,0)),vec(255),"models/flak/part/stands/century/selector.mdl")
    Thetas[6,number] = 0
    #Auto Brake
    holoCreate(7,CS:toWorld(vec(16.5,5.5,26.25)),vec(1),CS:angles(),vec(255),"models/flak/part/stands/century/trainbrake.mdl")
    holoEntity(7):setSubMaterial(2,"models/proppertextures/brakehandle")
    Thetas[7,number] = 0
    #Loco Brake
    holoCreate(8,CS:toWorld(vec(17,6.25,14)),vec(1),CS:toWorld(ang(0,60,0)),vec(255),"models/flak/part/stands/century/locobrake.mdl")
    Thetas[8,number] = 60
    
    for(N=4,13){
        holoParent(N,CS)
    }
    
    if(HornType){
        if(CS:lookupAttachment("horncord1")){
            local Hpos1 = CS:attachmentPos("horncord1")
            local Hang1 = CS:attachmentAng("horncord1")
            rangerFilter(CS)
            local Hpos2 = rangerOffset(128,Hpos1 + CordExtendLength*Hang1:right(), Hang1:up()):pos() 
            CordAnchor1 = CS:toLocal(Hpos2)
            local Hpos3 = linpos(Hpos1,Hpos2,DongleHeight) + (CordExtendLength/4)*Hang1:right()
            #local Haxis1 = (Hpos2-Hpos1)
            local Length1 = Hpos1:distance(Hpos3)
            local Ang1 = -heading(Hpos1,ang(),Hpos3)
            local Ang1a = Ang1:rotateAroundAxis(Ang1:forward(),-90)
            
            local Length2 = Hpos2:distance(Hpos3)
            local Ang2 = -heading(Hpos3,ang(),Hpos2)
            local Ang2a = Ang2:rotateAroundAxis(Ang2:right(),-90)
            
            holoCreate(1,linpos(Hpos1,Hpos3,0.5),vec(0.125,0.125,(Length1/60)),Ang1a,vec(255,225,127),"models/sprops/cylinders/size_1/cylinder_1_5x60.mdl")
            holoCreate(2,linpos(Hpos2,Hpos3,0.5),vec(0.125,0.125,(Length2/60)),Ang2a,vec(255,225,127),"models/sprops/cylinders/size_1/cylinder_1_5x60.mdl")
            holoCreate(3,Hpos3 + 1*Ang2a:up(),vec(0.125,0.125,0.2),Ang2a,vec(255),"models/sprops/misc/fittings/cred_12_9_tall.mdl")
            holoParent(1,CS)
            holoParent(2,CS)
            holoParent(3,CS)
            holoMaterial(1,"models/debug/debugwhite")
            holoMaterial(2,"models/debug/debugwhite")
            holoMaterial(3,"sprops/textures/sprops_wood2")
            
            C1P = CS:toLocal(Hpos1)
            C1A = CS:toLocal(Hang1)
            
            CS:setBodygroup(3,0)
        }
        #CS:setBodygroup(6,3)
    }else{
        CS:setBodygroup(3,1)
        holoCreate(1,CS:toWorld(vec(-3,5,27)),vec(1),CS:toWorld(ang(0,90,0)),vec(255),"models/flak/part/stands/century/hornlever.mdl")
        holoParent(1,CS)
    }
    
    CtlTable["CTL_Version",string] = CTL_Version
    setName("RLC PT2 Control Stand ("+CtlTable["CS_Version",string]+")\nALCo Century\n"+select(AirBrakeMode+1,"Simple SL","Simple ML","26-L","24-RL","No.6")+" with "+(LocoBrakeMode ? "ML" : "SL")+" Independents")
    runOnChat(1)
    timer("clk",100)
}
if(MU_Master){
    #Reverser Show
    if(!ReverserAlphaQ){
        ReverserAlphaQ = 1
        holoAlpha(4,255)
    }
    #MASTER MODE
    local Driver = noentity()
    local PodDriver = Pod:driver()
    if(RemoteControl | RemoteMode){
        if(Active & !PodDriver){
            Driver = owner()
        }
    }else{
        Driver = Pod:driver()
    }
    if(Driver & !DriverQ){
        DriverQ = 1
        runOnKeys(Driver,1)
    }elseif(!Driver & DriverQ){
        DriverQ = 0
        runOnKeys(DriverOld,0)
    }
    DriverOld = Driver
    
    if(keyClk(Driver)){
        local Shifting = Driver:keyPressed(Shift)
        
        UseButton = Driver:keyPressed(Controls["UseButton",string]) & (Routing["UseButton",number] ? Shifting : !Shifting)
        
        if(!UseButton){
        
            local RF = UsingRearSeat ? "ReverserB" : "ReverserF"
            local RB = UsingRearSeat ? "ReverserF" : "ReverserB"
            
            ReverserF = Driver:keyPressed(Controls[RF,string]) & (Routing[RF,number] ? Shifting : !Shifting)
            ReverserB = Driver:keyPressed(Controls[RB,string]) & (Routing[RB,number] ? Shifting : !Shifting)
            
            IncreaseThrottle = Driver:keyPressed(Controls["IncreaseThrottle",string]) & (Routing["IncreaseThrottle",number] ? Shifting : !Shifting)
            DecreaseThrottle = Driver:keyPressed(Controls["DecreaseThrottle",string]) & (Routing["DecreaseThrottle",number] ? Shifting : !Shifting)
            
            IncreaseTrainBrake = Driver:keyPressed(Controls["IncreaseTrainBrake",string]) & (Routing["IncreaseTrainBrake",number] ? Shifting : !Shifting)
            DecreaseTrainBrake = Driver:keyPressed(Controls["DecreaseTrainBrake",string]) & (Routing["DecreaseTrainBrake",number] ? Shifting : !Shifting)
            
            IncreaseLocoBrake = Driver:keyPressed(Controls["IncreaseLocoBrake",string]) & (Routing["IncreaseLocoBrake",number] ? Shifting : !Shifting)
            DecreaseLocoBrake = Driver:keyPressed(Controls["DecreaseLocoBrake",string]) & (Routing["DecreaseLocoBrake",number] ? Shifting : !Shifting)
            
            KeyDynUp = Driver:keyPressed(Controls["IncreaseDynamics",string]) & (Routing["IncreaseDynamics",number] ? Shifting : !Shifting)
            KeyDynDn = Driver:keyPressed(Controls["DecreaseDynamics",string]) & (Routing["DecreaseDynamics",number] ? Shifting : !Shifting)
            
            IncreaseDynamics = 0
            DecreaseDynamics = 0
            
            EmergencyBrake = Driver:keyPressed(Controls["EmergencyBrake",string]) & (Routing["EmergencyBrake",number] ? Shifting : !Shifting)
            
            HandBrake = Driver:keyPressed(Controls["HandBrake",string]) & (Routing["HandBrake",number] ? Shifting : !Shifting)
            Sander = Driver:keyPressed(Controls["Sander",string]) & (Routing["Sander",number] ? Shifting : !Shifting)
            
            SelectorUp = Driver:keyPressed(Controls["SelectorUp",string]) & (Routing["SelectorUp",number] ? Shifting : !Shifting)
            SelectorDown = Driver:keyPressed(Controls["SelectorDown",string]) & (Routing["SelectorDown",number] ? Shifting : !Shifting)
            
        }
    }
}else{
    #Reverser Show
    if(ReverserAlphaQ){
        ReverserAlphaQ = 0
        holoAlpha(4,0)
    }
    if(DriverQ){
        DriverQ = 0
        runOnKeys(owner(),0)
    }
}
if(clk("clk")){
    local Tick = 1
    Dynamics = SetDynamics>0
    
    
    #Lever-Based Controlling - This part is different for every control stand!
    if(LeverBased){
        #Initial Control Resets
        
        #Selector/Dynamics Interlock
        if(!SelectorQ & !DNQ){
            if((SetSelector>=0) & !Dynamics & SelectorDown){
                SelectorUp = 1
                SelectorDown = 0
            }elseif((SetSelector>0) & !Dynamics & SelectorUp){
                SelectorUp = 0
                SelectorDown = 1
            }elseif((SetSelector==0) & !Dynamics & SelectorUp){
                SelectorUp = 0
                IncreaseDynamics = 1
            }elseif(Dynamics & (SetDynamics==10) & SelectorDown){
                SelectorDown = 0
                DecreaseDynamics = 1
            }elseif(Dynamics & (SetDynamics>=10) & SelectorUp){
                SelectorUp = 0
                IncreaseDynamics = 1
            }elseif(Dynamics & (SetDynamics>10) & SelectorDown){
                SelectorDown = 0
                DecreaseDynamics = 1
            }
        }
    }else{
        IncreaseDynamics = KeyDynUp
        DecreaseDynamics = KeyDynDn
    }
    
    #Throttle (modded behaviour)
    if(!Dynamics){
        if(NotchedThrottle){
            if(IncreaseThrottle & !NQ & (SetSelector>0)){
                if((Notch<NumThrottleNotches-1) & (Notch>0)){
                NQ = 1
                Notch++
                soundStop(1)
                Pod:soundPlay(1,3,Lever_ThrottleNotchUp)
                }elseif(Notch==NumThrottleNotches-1){
                NQ = 1
                Notch++
                soundStop(1)
                Pod:soundPlay(1,3,Lever_ThrottleMax)
                }elseif(Notch<1){
                NQ = 1
                Notch++
                soundStop(1)
                Pod:soundPlay(1,3,Lever_ThrottleOn)
                }
            }elseif(DecreaseThrottle & !NQ){
                if(Notch>1){
                NQ = 1
                Notch--
                soundStop(1)
                Pod:soundPlay(1,3,Lever_ThrottleNotchDown)
                }elseif(Notch==1){
                NQ = 1
                Notch--
                soundStop(1)
                Pod:soundPlay(1,3,Lever_ThrottleOff)
                }
            }elseif(!IncreaseThrottle & !DecreaseThrottle & NQ){
                NQ = 0
                #soundStop(1)
            }
            SetThrottle = dac(0,100,NumThrottleNotches,Notch)
        }else{
            SetThrottle += (IncreaseThrottle - DecreaseThrottle)*5
            SetThrottle = clamp(SetThrottle,0,100)
            Notch = adc(0,100,NumThrottleNotches,SetThrottle)
        }
    }
    
    #Hump Control
    
    SetHumpMul = ->HumpMultiplier ? HumpMultiplier : 1
    
    #Dynamic Brakes
    if((SetThrottle==0) & HasDynamics & (abs(SetReverser)==1)){
        if((SetDynamics==0) & IncreaseDynamics & !DNQ){
            DNQ = 1
            DynaNotch = 0
            SetDynamics = 10
            soundStop(1)
            Pod:soundPlay(1,3,Lever_ThrottleNotchUp)
        }elseif((SetDynamics==10) & DecreaseDynamics & !DNQ){
            DNQ = 1
            DynaNotch = -1
            SetDynamics = 0
            soundStop(1)
            Pod:soundPlay(1,3,Lever_ThrottleOff)
        }
        
        if(Dynamics & NotchedDynamics){
            if(IncreaseDynamics & !DNQ){
                if((DynaNotch<NumThrottleNotches-1)){
                    DNQ = 1
                    DynaNotch++
                    soundStop(1)
                    Pod:soundPlay(1,3,Lever_ThrottleNotchUp)
                }elseif(DynaNotch==NumThrottleNotches-1){
                    DNQ = 1
                    DynaNotch++
                    soundStop(1)
                    Pod:soundPlay(1,3,Lever_ThrottleMax)
                }
            }elseif(DecreaseDynamics & !DNQ){
                    if(DynaNotch>0){
                    DNQ = 1
                    DynaNotch--
                    soundStop(1)
                    Pod:soundPlay(1,3,Lever_ThrottleNotchDown)
                }elseif(DynaNotch==0){
                    NQ = 1
                    DynaNotch--
                    soundStop(1)
                    Pod:soundPlay(1,3,Lever_ThrottleOff)
                }
            }
            if(DynaNotch>-1){SetDynamics = dac(10,100,NumThrottleNotches,DynaNotch)}
        }elseif(Dynamics){
            if(IncreaseDynamics & (SetDynamics<100) & !DNQ){
                SetDynamics++
                if(SetDynamics>99){
                    soundStop(1)
                    Pod:soundPlay(1,3,Lever_ThrottleMax)
                }
                if(SetDynamics>100){
                    SetDynamics=100}
            }elseif(DecreaseDynamics & (SetDynamics>10)){
                DNQ = 1
                SetDynamics--
                if(SetDynamics==11){
                    soundStop(1)
                    Pod:soundPlay(1,3,Lever_ThrottleNotchDown)
                }
                if(SetDynamics<10){
                    SetDynamics=10
                    DNQ=1
                }
            }
            DynaNotch = adc(10,100,NumThrottleNotches,SetDynamics)
        }
        if(!IncreaseDynamics & !DecreaseDynamics & DNQ){
            DNQ = 0
        }
    }
    
    #Selector
    if((TransitionMode==1) & (TransitionControlType<2)){
        if(SelectorUp & !SelectorQ){
            SelectorQ = 1
            if(!Dynamics & ((SetSelector==0) | ((abs(SetReverser)) & (SetSelector<4)))){
                SetSelector++
                soundStop(1)
                Pod:soundPlay(1,3,(SetSelector>3 ? Lever_ThrottleMax : Lever_ThrottleNotchUp))
            }
        }elseif(SelectorDown & !SelectorQ){
            SelectorQ = 1
            if(!Dynamics & ((SetSelector==1) | ((abs(SetReverser) & (SetSelector>0))))){
                SetSelector--
                soundStop(1)
                Pod:soundPlay(1,3,(SetSelector>0 ? Lever_ThrottleNotchDown : Lever_ThrottleOff))
            }
        }elseif(!SelectorUp & !SelectorDown & SelectorQ){
            SelectorQ = 0
        }
        
    }
    
    #Reverser
    
    if((SetThrottle==0) & (!Dynamics)){
        if(ReverserF & !RevQ & (SetReverser<1) & (SetSelector<=1)){
            RevQ = 1
            SetReverser++
            soundStop(1)
            Pod:soundPlay(1,3,(SetReverser==0) ? Lever_ReverserN : Lever_ReverserF)
        }elseif(ReverserB & !RevQ & (SetReverser>-1) & (SetSelector<=1)){
            RevQ = 1
            SetReverser--
            soundStop(1)
            Pod:soundPlay(1,3,(SetReverser==0) ? Lever_ReverserN : Lever_ReverserB)
        }elseif(!ReverserF & !ReverserB & RevQ){
            RevQ = 0
        }
    }
    
    #Train Brakes
    local FSP = FeedPressure*64/90 #Full Service Point
    
    if(AirBrakeMode==4){ #6SL
        #0-Release
        #1-Running
        #2-Holding
        #3-Lap
        #4-Service
        #6-Emergency
        if(IncreaseTrainBrake & (SetBrakeNotch < 4) & !ABQ){
            ABQ = 1
            SetBrakeNotch++
            soundStop(2)
            CS:soundPlay(2,5,Lever_ABNotch)
            if(SetBrakeNotch==4 & (SetEqualRes>FSP) & !HissQ){
                HissQ = 1
                CS:soundPlay(3,10,AirBrakeApply)
                timer("EndHiss",6000)
            }
        }elseif(DecreaseTrainBrake & (SetBrakeNotch > 0) & !ABQ){
            ABQ = 1
            if(SetBrakeNotch==6){
                SetBrakeNotch=4
            }else{
                SetBrakeNotch--
            }
            soundStop(2)
            CS:soundPlay(2,5,Lever_ABNotch)
            if(SetBrakeNotch<=1 & (SetEqualRes<FeedPressure) & !HissQ){
                HissQ = 1
                CS:soundPlay(3,10,AirBrakeRelease)
                timer("EndHiss",6000)
            }
        }elseif(!IncreaseTrainBrake & !DecreaseTrainBrake & ABQ){
            #soundStop(3)
            ABQ = 0
        }
        switch(SetBrakeNotch){
            case 0, #Release
                if(SetEqualRes<FeedPressure){
                    SetEqualRes+=0.5
                    if(SetEqualRes>FeedPressure){SetEqualRes=FeedPressure}
                }
                break
            case 1, #Running
                if(SetEqualRes<FeedPressure){
                    SetEqualRes+=0.5
                    if(SetEqualRes>FeedPressure){SetEqualRes=FeedPressure}
                }
                break
            case 2, #Holding
                if(SetEqualRes<FeedPressure){
                    SetEqualRes+=0.5
                    if(SetEqualRes>FeedPressure){SetEqualRes=FeedPressure}
                }
                break
            case 4, #Service
                if(SetEqualRes>FSP){
                    SetEqualRes-0.5
                    if(SetEqualRes<FSP){SetEqualRes=FSP}
                }
                break
            default,
                break
        }
    }elseif(AirBrakeMode==3){ #24RL
        #0-Release
        #1-Running
        #2-First Service
        #3-Lap
        #4-Service
        #6-Emergency
        if(IncreaseTrainBrake & (SetBrakeNotch < 4) & !ABQ){
            ABQ = 1
            SetBrakeNotch++
            soundStop(2)
            CS:soundPlay(2,5,Lever_ABNotch)
            if(SetBrakeNotch==4 & (SetEqualRes>FSP) & !HissQ){
                HissQ = 1
                CS:soundPlay(3,10,AirBrakeApply)
                timer("EndHiss",6000)
            }elseif(SetBrakeNotch==2 & (SetEqualRes>(FeedPressure-InitialReduction))){
                SetEqualRes = FeedPressure-InitialReduction
                HissQ = 1
                CS:soundPlay(3,10,AirBrakeApply)
                timer("EndHiss",6000)
            }
        }elseif(DecreaseTrainBrake & (SetBrakeNotch > 0) & !ABQ){
            ABQ = 1
            if(SetBrakeNotch==6){
                SetBrakeNotch=4
            }else{
                SetBrakeNotch--
            }
            soundStop(2)
            CS:soundPlay(2,5,Lever_ABNotch)
            if(SetBrakeNotch<=1 & (SetEqualRes<FeedPressure) & !HissQ){
                HissQ = 1
                CS:soundPlay(3,10,AirBrakeRelease)
                timer("EndHiss",6000)
            }
        }elseif(!IncreaseTrainBrake & !DecreaseTrainBrake & ABQ){
            #soundStop(3)
            ABQ = 0
        }
        switch(SetBrakeNotch){
            case 0, #Release
                if(SetEqualRes<FeedPressure){
                    SetEqualRes+=0.5
                    if(SetEqualRes>FeedPressure){SetEqualRes=FeedPressure}
                }
                break
            case 1, #Running
                if(SetEqualRes<FeedPressure){
                    SetEqualRes+=0.5
                    if(SetEqualRes>FeedPressure){SetEqualRes=FeedPressure}
                }
                break
            case 4, #Service
                if(SetEqualRes>FSP){
                    SetEqualRes-=0.5
                    if(SetEqualRes<FSP){SetEqualRes=FSP}
                }
                break
            default,
                break
        }
        
    }elseif(AirBrakeMode==2){#26L
    #0-Release
    #1-Minimum Reduction / Service
    #2-Suppression
    #6-Emergency
        if(IncreaseTrainBrake){
            if(!ABQ){
                ABQ = 1
                if(SetBrakeNotch==0){
                    Lock_26L = 1
                    SetBrakeNotch=1
                    soundStop(2)
                    CS:soundPlay(2,5,Lever_ABNotch)
                    CS:soundPlay(3,10,AirBrakeApplyInitial)
                    SetEqualRes = FeedPressure-InitialReduction
                }elseif((SetBrakeNotch==1) & (SetEqualRes>FSP)){
                    soundStop(2)
                    CS:soundPlay(2,5,Lever_ABMove)
                    HissQ = 1
                    CS:soundPlay(3,10,AirBrakeApply)
                    timer("EndHiss",6000)
                }elseif((SetBrakeNotch==1) & (SetEqualRes==FSP)){
                    SetBrakeNotch=2
                    soundStop(2)
                    CS:soundPlay(2,5,Lever_ABSuppressionOn)
                    Suppression = 1
                }
            }
            if((SetBrakeNotch==1) & (SetEqualRes>FSP) & !Lock_26L){
                SetEqualRes-=0.5
                if(SetEqualRes<FSP){SetEqualRes=FSP}
            }
        }elseif(DecreaseTrainBrake){
            if(!ABQ){
                ABQ = 1
                if((SetBrakeNotch==1) & (SetEqualRes==(FeedPressure-InitialReduction))){
                    SetBrakeNotch=0
                    soundStop(2)
                    CS:soundPlay(2,5,Lever_ABRelease)
                    SetEqualRes = FeedPressure
                    if(!HissQ){
                        HissQ = 1
                        CS:soundPlay(3,10,AirBrakeRelease)
                        timer("EndHiss",6000)
                    }
                }elseif((SetBrakeNotch==1) & (SetEqualRes<FeedPressure)){
                    soundStop(2)
                    CS:soundPlay(2,5,Lever_ABMove)
                    if(!HissQ & (SetEqualRes<FeedPressure)){
                        HissQ = 1
                        CS:soundPlay(3,10,AirBrakeRelease)
                        timer("EndHiss",6000)
                    }
                }elseif((SetBrakeNotch==2)){
                    Lock_26L = 1
                    SetBrakeNotch=1
                    soundStop(2)
                    CS:soundPlay(2,5,Lever_ABSuppressionOff)
                    Suppression = 0
                }elseif(SetBrakeNotch==6){
                    SetBrakeNotch=2
                    soundStop(2)
                    soundStop(3)
                    CS:soundPlay(2,10,Lever_ABEmergencyRecover)
                    Suppression = 1
                }
                
            }
            if((SetBrakeNotch==1) & (SetEqualRes<(FeedPressure-InitialReduction)) & !Lock_26L){
                SetEqualRes+= 1.5 #Triple Release Speed
                if(SetEqualRes>=(FeedPressure-InitialReduction)){
                    SetEqualRes = FeedPressure - InitialReduction
                    ABQ = 0
                    }
            }
        }elseif(!IncreaseTrainBrake & !DecreaseTrainBrake & ABQ){
            ABQ = 0
            Lock_26L = 0
        }
    }elseif(AirBrakeMode==1){#Simple Manual Lapping
    #0-Release
    #1-Lap
    #2-Apply
    #6-Emergency
        if(IncreaseTrainBrake & !ABQ & (SetBrakeNotch<2)){
            ABQ = 1
            SetBrakeNotch++
            soundStop(2)
            CS:soundPlay(2,5,Lever_ABMove)
            if((SetBrakeNotch==2) & (SetEqualRes>FSP) & !HissQ){
                HissQ = 1
                CS:soundPlay(3,10,AirBrakeApply)
                timer("EndHiss",6000)
            }
        }elseif(DecreaseTrainBrake & !ABQ & (SetBrakeNotch>0)){
            ABQ = 1
            if(SetBrakeNotch==6){
                SetBrakeNotch=2
                soundStop(2)
                CS:soundPlay(2,10,Lever_ABEmergencyRecover)
            }else{
                SetBrakeNotch--
            }
            soundStop(2)
            CS:soundPlay(2,5,Lever_ABMove)
            if((SetBrakeNotch==0) & (SetEqualRes<FeedPressure) & !HissQ){
                HissQ = 1
                CS:soundPlay(3,10,AirBrakeRelease)
                timer("EndHiss",6000)
            }
        }elseif(!IncreaseTrainBrake & !DecreaseTrainBrake & ABQ){
            ABQ = 0
        }
        

        if((SetBrakeNotch==2) & (SetEqualRes>FSP)){
            SetEqualRes-=0.25
            if(SetEqualRes<FSP){SetEqualRes=FSP}
        }elseif((SetBrakeNotch==0) & (SetEqualRes<FeedPressure)){
            SetEqualRes+=0.25
            if(SetEqualRes>FeedPressure){SetEqualRes=FeedPressure}
        }
        
    }else{#Simple Self Lapping
            
    #Lol what notches?
    #0-Service Region
    #6-Emergency
        if(IncreaseTrainBrake & !ABQ){
            ABQ = 1
            soundStop(2)
            CS:soundPlay(2,5,Lever_ABMove)
            if((SetEqualRes>FSP) & !HissQ){
                HissQ = 1
                CS:soundPlay(3,10,AirBrakeApply)
                timer("EndHiss",6000)
            }
        }elseif(DecreaseTrainBrake & !ABQ){
            ABQ = 1
            if(SetBrakeNotch==6){
                SetBrakeNotch = 0
                soundStop(2)
                CS:soundPlay(2,10,Lever_ABEmergencyRecover)
            }else{
                soundStop(2)
                CS:soundPlay(2,5,Lever_ABMove)
                if((SetEqualRes<FeedPressure) & !HissQ){
                    HissQ = 1
                    CS:soundPlay(3,10,AirBrakeRelease)
                    timer("EndHiss",6000)
                }
            }
        }elseif(!IncreaseTrainBrake & !DecreaseTrainBrake & ABQ){
            ABQ = 0
        }

            
        if(IncreaseTrainBrake & (SetEqualRes>FSP)){
            SetEqualRes-=0.25
            BleedPressure = Bleeding ? 0 : FeedPressure
            if(SetEqualRes<FSP){SetEqualRes=FSP}
        }elseif(DecreaseTrainBrake & (SetEqualRes<FeedPressure)){
            SetEqualRes+=0.25
            if(SetEqualRes>FeedPressure){SetEqualRes=FeedPressure}
        }
        
    }
    
    #Locomotive Brakes
    if(!LocoBrakeMode){ #Self Lapping
        if(IncreaseLocoBrake & (SetLocoBrakePressure<60)){
            #if(BleedPressure<72){BleedPressure=72}
            if(!LBQ){LBQ=1}
            SetLocoBrakePressure+=4
            if(SetLocoBrakePressure>60){
                SetLocoBrakePressure = 60
            }
            if(SetLocoBrakePressure==60 & !LHissQ){
                LHissQ = 1
                soundStop(1)
                Pod:soundPlay(1,3,LocoBrakeFull)
                timer("EndHiss",500)
            }
        }elseif(DecreaseLocoBrake & (SetLocoBrakePressure>0)){
            if(!LBQ){LBQ=1}
            SetLocoBrakePressure-=4
            if(SetLocoBrakePressure<0){SetLocoBrakePressure = 0}
            if(!LHissQ){
                LHissQ = 1
                Pod:soundPlay(2,6,(SetLocoBrakePressure>30) ? LocoBrakeReleaseHard : LocoBrakeReleaseSoft)
                timer("EndHiss",6000)
            }
        }elseif(DecreaseLocoBrake & (SetLocoBrakePressure==0) & !LBQ){
            LBQ = 1
            SetBleeding = 1
            if((SetEqualRes<FeedPressure) & !LHissQ){
                LHissQ = 1
                Pod:soundPlay(2,6,(SetEqualRes<30) ? LocoBrakeReleaseHard : LocoBrakeReleaseSoft)
                timer("EndHiss",6000)
            }
        }elseif(!IncreaseLocoBrake & !DecreaseLocoBrake & LBQ){
            LBQ = 0
            SetBleeding = 0
        }

    }else{ #Manual Lapping
            
        if(IncreaseLocoBrake & !LBQ & (SetLocoBrakeNotch<2)){
            LBQ = 1
            SetLocoBrakeNotch++
            if(SetLocoBrakeNotch==2){
                soundStop(1)
                Pod:soundPlay(1,3,LocoBrakeFull)
                #BleedPressure = 72
            }
        }elseif(DecreaseLocoBrake & !LBQ & (SetLocoBrakeNotch>0)){
            LBQ = 1
            SetLocoBrakeNotch--
            if((SetLocoBrakeNotch<=0) & (SetLocoBrakePressure>0) & !LHissQ){
                LHissQ = 1
                Pod:soundPlay(2,6,(SetLocoBrakePressure>30) ? LocoBrakeReleaseHard : LocoBrakeReleaseSoft)
                timer("EndHiss",6000)
            }
        }elseif(DecreaseLocoBrake & !LBQ & (SetLocoBrakeNotch==0)){
            LBQ = 1
            SetBleeding = 1
            if((SetLocoBrakeNotch<=0) & (SetEqualRes<FeedPressure) & !LHissQ){
                LHissQ = 1
                Pod:soundPlay(2,6,(SetEqualRes<60) ? LocoBrakeReleaseHard : LocoBrakeReleaseSoft)
                timer("EndHiss",6000)
            }
        }elseif(!IncreaseLocoBrake & !DecreaseLocoBrake & LBQ){
            LBQ = 0
            SetBleeding = 0
            
        }
       
        if(SetBleeding & (SetLocoBrakePressure>0)){
            SetLocoBrakePressure-=4
            if(SetLocoBrakePressure<0){SetLocoBrakePressure=0}
        }elseif((SetLocoBrakeNotch==2) & (SetLocoBrakePressure<60)){
            SetLocoBrakePressure+=2
            if(SetLocoBrakePressure>60){SetLocoBrakePressure=60}
        }elseif((SetLocoBrakeNotch<=0) & (SetLocoBrakePressure>0)){
            SetLocoBrakePressure-=2
            if(SetLocoBrakePressure<0){SetLocoBrakePressure=0}
        }
        
    }
    
    #Emergency Brake
    
    if(EmergencyBrake | EmBrake){
        
        if(SetBrakeNotch<6){
            CS:soundPlay(2,0,Lever_ABEmergency)
        }
        
        SetEqualRes = 0
        SetThrottle = 0
        Notch = 0
        SetDynamics = 0
        DynaNotch = 0
        SetBrakeNotch = 6
        
    }
        
    #Control Stand
    
    #Hooloos
    local Ang = 0
    
    #Reverser
    if(SetReverser==1){
        Ang = 135
    }elseif(SetReverser==-1){
        Ang = 45
    }else{
        Ang = 90
    }
    lever(4,Ang,20)
    
    #Throttle and Notch Wheel
    Ang = 118 - SetThrottle*65/100
    lever(5,Ang,10)
    
    CS:setBodygroup(1,Notch)
    
    #Selector and Selector Wheel
    if(SetDynamics>0){
        Ang = 10 + (SetDynamics-10)*93/90
    }else{
        Ang = 0 - SetSelector*10
    }
    lever(6,Ang,10)
    
    if(SetSelector>0){
        CS:setBodygroup(2,SetSelector+1)
    }else{
        CS:setBodygroup(2,0)
    }
    
    if(SetDynamics>0){
        CS:setBodygroup(2,1)
    }
    
    #Automatic Brake
    if(AirBrakeMode==2){
        if(SetBrakeNotch==0){
            Ang = 0
        }
        if(SetBrakeNotch==1){
            Ang = 135-(SetEqualRes*1.5)
        }
        if(SetBrakeNotch==2){
            Ang = 150
        }
        if(SetBrakeNotch==6){
            Ang = 180
        }
    }else{
    Ang = select(SetBrakeNotch+1, 0, 20, 41, 68, 90, 108)
    }
    local Speed = (SetBrakeNotch==6) ? 30 : 15
    lever(7,Ang,Speed)
    
    #Locomotive Brake
    if(SetBleeding | (LocoBrakePitch!=0)){
        lever(8,60,10*SetBleeding,20)
    }else{
        lever(8,60 + 100*SetLocoBrakePressure/60,15)
    }
    
    #Horn 1
    if(HornType){
        local Hpos1 = CS:toWorld(C1P)
        local Hang1 = CS:toWorld(C1A)
        #CordDir1 = CS:toLocal(Hang1:forward())
        #rangerFilter(CS)
        local Hpos2 = CS:toWorld(CordAnchor1)
        local PFactor = Quill1 ? 0.5*Horn1 : Horn1
        local Hpos3 = linpos(Hpos1,Hpos2,DongleHeight) + Hang1:forward()*(CordExtendLength/4 + PullLength*PFactor) - Hang1:up()*PullLength*PFactor*DownRatio
        #local Haxis1 = (Hpos2-Hpos1)
        local Length1 = Hpos1:distance(Hpos3)
        local Ang1 = -heading(Hpos1,ang(),Hpos3)
        local Ang1a = Ang1:rotateAroundAxis(Ang1:right(),-90)
        local Length2 = Hpos2:distance(Hpos3)
        local Ang2 = -heading(Hpos3,ang(),Hpos2)
        local Ang2a = Ang2:rotateAroundAxis(Ang2:right(),-90)
        
        #holoCreate(6,linpos(Hpos1,Hpos3,0.5),vec(0.25,0.25,(Length1/60)),Ang1a,vec(255,225,127),"models/sprops/cylinders/size_1/cylinder_1_5x60.mdl")
        #holoCreate(7,linpos(Hpos2,Hpos3,0.5),vec(0.25,0.25,(Length2/60)),Ang2a,vec(255,225,127),"models/sprops/cylinders/size_1/cylinder_1_5x60.mdl")
        holoScale(1,vec(0.125,0.125,(Length1/60)))
        holoScale(2,vec(0.125,0.125,(Length2/60)))
        holoPos(1,linpos(Hpos1,Hpos3,0.5))
        holoPos(2,linpos(Hpos2,Hpos3,0.5))
        holoPos(3,Hpos3 + 1*Ang2a:up())
        holoAng(1,Ang1a)
        holoAng(2,Ang2a)
    }else{
    
        if(Horn1){
            holoAng(1,CS:toWorld(ang(15,90,0)))
        }elseif(Quill1){
            holoAng(1,CS:toWorld(ang(5,90,0)))
        }else{
            holoAng(1,CS:toWorld(ang(0,90,0)))
        }
        
    }
    #Horn 2 Unused
    #CS:setBodygroup(1,MU_Master ? SetReverser+2 : 0)
    
    timer("clk",50)
}elseif(clk("EndHiss")){
    HissQ = 0
    LHissQ = 0
}elseif(clk("PenaltyTimeout")){
    PenaltyBrake = 0   
}

local InRelease = ((AirBrakeMode==3)|(AirBrakeMode==4)) & (SetBrakeNotch==0)
if(InRelease & !WarningHissQ){
    WarningHissQ = 1
    CS:soundPlay(3,0,WarningHiss)
}elseif(!InRelease & WarningHissQ){
    WarningHissQ = 0
    soundStop(3,0.125)
}

CtlTable["SetReverser",number] = SetReverser
CtlTable["SetThrottle",number] = clamp(SetThrottle,0,->EC_Clamp ? EC_Clamp : 100)
CtlTable["SetDynamics",number] = SetDynamics
CtlTable["SetSelector",number] = SetSelector
CtlTable["SetBrakeNotch",number] = SetBrakeNotch
CtlTable["SetEqualRes",number] = SetEqualRes
CtlTable["SetHolding",number] = (AirBrakeMode==4)&(SetBrakeNotch==2)
CtlTable["Overcharge",number] = (SetBrakeNotch==0) & ((AirBrakeMode==3)|(AirBrakeMode==4))
CtlTable["FRC",number] = FullReleaseCock
#CtlTable["SetLocoBrakeNotch",number] = SetLocoBrakeNotch
CtlTable["SetLocoBrakePressure",number] = SetLocoBrakePressure
CtlTable["SetBleeding",number] = SetBleeding
CtlTable["SetHumpMultiplier",number] = SetHumpMul
CtlTable["SetFeedPressure",number] = FeedPressure

CtlTable["EmergencyBrake",number] = EmergencyBrake
CtlTable["HandBrake",number] = HandBrake
CtlTable["Sander",number] = Sander

if(chatClk(owner())|chatClk(Pod:driver())){
    local Speaker = lastSpoke()
    local Sentence = Speaker:lastSaid():explode(" ")
    local CMD_PreNum = Sentence[1,string]
    local CMD_S = CMD_PreNum:explode(":")
    local CMD = CMD_S[1,string]
    local NumberGiven = CMD_S[2,string]
    local Num = Number:trim()
    
    local OnRun = 1#(On1 & Run)|(On2 & Run_DPM)
    
    
    if((NumberGiven==Num) | NumberGiven==""){
        switch(CMD){
            case ".controls",
                if(MU_Master){
                    for(K=1,ControlPrintOrder:count()){
                        local ControlName = ControlPrintOrder[K,string]
                        local X = Controls[ControlName,string]
                        if(Routing[ControlName,number]){
                            Speaker:printProper(ControlName + ": Shift + " + X)
                        }else{
                            Speaker:printProper(ControlName + ": " + X)
                        }
                    }
                }
                hideChat(1)
                break
            case ".reverser",
                if(MU_Master & OnRun){
                    if(Dynamics){
                        Speaker:printProper("[RLCPT] Dynamic Brakes are On!")
                    }elseif(SetThrottle>0){
                        Speaker:printProper("[RLCPT] Throttle is On!")
                    }else{
                        SetReverser = sign(Sentence[2,string]:toNumber())
                        Speaker:printProper("[RLCPT] Reverser set to " + SetReverser:toString() + ".")
                    }
                }
                hideChat(1)
                break
            case ".throttle",
                if(MU_Master & OnRun){
                    if(Dynamics){
                        Speaker:printProper("[RLCPT] Dynamic Brakes are On!")
                    }elseif(HEP_Notch==1){
                        Speaker:printProper("[RLCPT] HEP Mode is in Standby!")
                    }else{
                        if(NotchedThrottle){
                            SetThrottle = clamp(dac(0,100,NumThrottleNotches,adc(0,100,NumThrottleNotches,Sentence[2,string]:toNumber())),0,100)
                            Notch = adc(0,100,NumThrottleNotches,SetThrottle)
                        }else{
                            SetThrottle = clamp(Sentence[2,string]:toNumber(),0,100)
                            Notch = adc(0,100,NumThrottleNotches,SetThrottle)
                        }
                        Speaker:printProper("[RLCPT] Throttle set to " + SetThrottle + "%.")
                    }
                }
                hideChat(1)
                break
            case ".notch",
                if(MU_Master & OnRun){
                    if(Dynamics){
                        Speaker:printProper("[RLCPT] Dynamic Brakes are On!")
                    }elseif(HEP_Notch==1){
                        Speaker:printProper("[RLCPT] HEP Mode is in Standby!")
                    }else{
                        if(NotchedThrottle){
                            Notch = clamp(round(Sentence[2,string]:toNumber()),0,NumThrottleNotches)
                            SetThrottle = dac(0,100,NumThrottleNotches,Notch)
                            Speaker:printProper("[RLCPT] Throttle set to Notch " + Notch + ".")
                            
                        }else{
                            Speaker:printProper("[RLCPT] Throttle has no Notches!")
                        }
                        
                    }
                }
                hideChat(1)
                break
            case ".dynamics",
                if(MU_Master & OnRun){
                    if(!HasDynamics){
                        Speaker:printProper("[RLCPT] Dynamic Brakes are not Enabled!")
                    }elseif(!abs(SetReverser)){
                        Speaker:printProper("[RLCPT] Reverser is not Engaged!")
                    }elseif(SetThrottle>0){
                        Speaker:printProper("[RLCPT] Throttle is not in Idle!")
                    }else{
                        local Set = clamp(Sentence[2,string]:toNumber(),0,100)
                        SetDynamics = (Set<10) ? 0 : Set
                        DynaNotch = (SetDynamics==0) ? -1 : adc(10,100,NumThrottleNotches,SetDynamics)
                        
                        #Dynamics = DynamicBrakingPower>0
                        
                        Speaker:printProper("[RLCPT] Dynamic Brake set to " + SetDynamics + "%.")
                    }
                }
                hideChat(1)
                break
            case ".dynanotch",
                if(MU_Master & OnRun){
                    if(NotchedDynamics){
                        if(!HasDynamics){
                            Speaker:printProper("[RLCPT] Dynamic Brakes are not Enabled!")
                        }elseif(!abs(SetReverser)){
                            Speaker:printProper("[RLCPT] Reverser is not Engaged!")
                        }elseif(SetThrottle>0){
                            Speaker:printProper("[RLCPT] Throttle is not in Idle!")
                        }else{
                            local Set = clamp(round(Sentence[2,string]:toNumber()),-1,NumThrottleNotches)
                            DynaNotch = Set
                            SetDynamics = (Set==-1) ? 0 : dac(10,100,NumThrottleNotches,DynaNotch)
                            
                            #Dynamics = DynamicBrakingPower>0
                            
                            Speaker:printProper("[RLCPT] Dynamic Brake set to Notch " + DynaNotch + " (" + SetDynamics + "%).")
                        }
                    }else{
                        Speaker:printProper("[RLCPT] Dynamic Brakes are not Notched!")
                    }
                }
                hideChat(1)
                break
            case ".abset",
                if(MU_Master & OnRun){
                    local What = Sentence[2,string]
                    if(What==""){
                        SetEqualRes = 0
                    				SetThrottle = 0
                    				Notch = 0
                    				SetDynamics = 0
                    				SetBrakeNotch = 6
                        Speaker:printProper("[RLCPT] Setting Emergency Brake application!")
                    }else{
                        local Set = clamp(What:toNumber(),0,FeedPressure*64/90)
                        switch(AirBrakeMode){
                            case 4, #6SL
                                if(Set==0){
                                    SetEqualRes = FeedPressure
                                    SetBrakeNotch = 1
                                }else{
                                    SetEqualRes = FeedPressure - Set
                                    SetBrakeNotch = 3
                                }
                                break
                            case 3, #24RL
                                if(Set==0){
                                    SetEqualRes = FeedPressure
                                    SetBrakeNotch = 1
                                }elseif(Set<InitialReduction){
                                    SetEqualRes = FeedPressure - InitialReduction
                                    SetBrakeNotch = 2
                                }else{
                                    SetEqualRes = FeedPressure - Set
                                    SetBrakeNotch = 3
                                }
                                break
                            case 2, #26L
                                if(Set==0){
                                    SetEqualRes = FeedPressure
                                    SetBrakeNotch = 0
                                }elseif(Set<InitialReduction){
                                    SetEqualRes = FeedPressure - InitialReduction
                                    SetBrakeNotch = 1
                                }else{
                                    SetEqualRes = FeedPressure - Set
                                    SetBrakeNotch = 1
                                }
                                break
                            case 1, #SML
                                SetBrakeNotch = 1
                                SetEqualRes = FeedPressure - Set
                                break
                            case 0, #SSL
                                SetBrakeNotch = 0
                                SetEqualRes = FeedPressure - Set
                                break
                        }
                        Speaker:printProper("[RLCPT] Setting " + (FeedPressure-SetEqualRes) + "lb application on train.")
                    }
                }
                hideChat(1)
                break
			case ".brake",
				if(MU_Master & OnRun){
					SetEqualRes = 0
					SetThrottle = 0
					Notch = 0
					SetDynamics = 0
					SetBrakeNotch = 6
					Speaker:printProper("[RLCPT] Setting Emergency Brake application!")
				}
				hideChat(1)
				break
            case ".lbset",
                local Set = clamp(Sentence[2,string]:toNumber(),0,72)
                local ML = (AirBrakeMode==1)|(AirBrakeMode==3)|(AirBrakeMode==4)
                SetLocoBrakePressure = Set
                if(ML){SetLocoBrakeNotch=1}
                Speaker:printProper("[RLCPT] Setting " + (SetLocoBrakePressure) + "lb application on locomotive(s).")
                
                hideChat(1)
                break
            case ".remotemode",
                if(MU_Master){
                    RemoteMode = sign(Sentence[2,string]:toNumber())
                    Speaker:printProper("[RLCPT] Remote Control Mode set to " + RemoteMode + ".")
                    hideChat(1)
                }
                break
            case ".feedvalve",
                if(Sentence[2,string]==""){
                    print("[RLCPT] Your Feed Valve is currently set to "+FeedPressure+" PSI.")
                }else{
                    FeedPressure = clamp(Sentence[2,string]:toNumber(),70,120)
                    print("[RLCPT] Your Feed Valve is now set to "+FeedPressure+" PSI.")
                }
                hideChat(1)
                break
            case ".fullreleasecock",
                if(AirBrakeMode==3){
                    if(Sentence[2,string]==""){
                        FullReleaseCock = 0
                    }else{
                        FullReleaseCock = Sentence[2,string]:toNumber()
                    }
                    print("[RLCPT] Your Full Release Cock is now set to " + (FullReleaseCock ? "MR (1)." : "FV (0)."))
                }else{
                    print("[RLCPT] You don't have a Full Release Cock!")
                }
                
                hideChat(1)
                break
            default,
                if(CMD:index(1)=="."){
                    local CMDL = CMD:lower()
                    if("abcdefghijklmnopqrstuvwxyz":find(CMDL:index(2))){
                        hideChat(1)
                    }
                }
                break
        }
    }
    
}
if(LightToggle & ~LightToggle){
    Light = !Light
    CS:soundPlay(0,1,"buttons/lightswitch2.wav")
}
if(BellToggle & ~BellToggle){
    Bell = !Bell
    CS:soundPlay(0,3,Bell ? "gsgtrainsounds/misc/air_switch.wav" : "moztrainsounds/us_horns_bells/bell_off.wav")
}
if(MiscToggle & ~MiscToggle){
    Misc = !Misc
}
