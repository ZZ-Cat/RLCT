@name magspack2/tcl/TCL Ditchlight Controller
@inputs Pod:entity NextWeapon PrevWeapon ThirdPerson
@outputs RGBF:vector RGBR:vector OnF OnR
@persist E:entity Driver:entity Position Unlock KeyUnlock SwitchSound:string UnlockKey:string Radius LightRGB:vector RockLights
@trigger NextWeapon PrevWeapon UnlockKey

# V1.0

#-------------------------------------------------------------------------------#
# W E L C O M E   A B O A R D   T H E   T R A N S - C A N A D A   L I M I T E D #
#                                                                               #
# Hello and welcome to TCL's Ditchlight Controller!                             #
#                                                                               #
# Setup:                                                                        #
#   1)  Spawn and position this E2 somewhere accessible from your engineer's    #
#       seat. ** It must be visible from the seat to be used. **                #
#   2)  Spawn, position and parent your ditch lights.                           #
#   3)  Wire all inputs on this E2 to their respective outputs on your Pod.     #
#   4)  Wire RGB and On outputs to your front (F) and rear (R) ditch lights.    #
#   5)  Cycle through the positions by either aiming at the E2 or holding down  #
#       the key you've set to unlock it, and roll your mouse wheel.             #
#   6)  Make sure you parent everything, and enjoy!                             #
#                                                                               #
# SUPER thanks to Monkatraz for help with development and optimization!         #
#                                                                               #
# Happy railroading!                                                     TCL <3 #
#                                                                               #
#         Having problems? Find me on Discord!  TransCanadaLimited#8427         #
#-------------------------------------------------------------------------------#

if(first() | dupefinished()){
    
SwitchSound = "trans_train_sounds/buttons/headlight_switch.wav"

UnlockKey = "3" #Hold down this key to operate the switch without looking at it.

SwitchScale = 1 #Scale of the switch model.

Radius = 2 #Radius your cursor will be detected from.

LightRGB = vec(255,255,200) #Light RGB value. (255,255,200 is the default used by RLC locos.)

RockLights = 0 #Set this to 1 if your locomotive has two sets of ditch lights or rock lights.
               #In this case, wire RGBF/R and OnF/R to the top and bottom sets of lights respectively.

####

E=entity()
if(E:parent():isValid()){
        E:setMaterial("models/proppertextures/invisible")
    }

holoCreate(0)
holoParent(0,E)
holoModel(0,"models/rusty/details/knob_modern.mdl")
holoScale(0,vec(SwitchScale))

timer("clk",100)

}

#Main Code

Driver = Pod:driver()
if(Driver){
Unlock = Driver:aimPos():distance(E:pos()) < Radius
KeyUnlock = Driver:keyPressed(UnlockKey)
}

if(inputClk() & ((Unlock & !ThirdPerson) | KeyUnlock)){
    if(PrevWeapon){ #Switch Pos Up
        soundPlay(0,1,SwitchSound)
        if(Position<3){
            Position++
        }elseif(Position==3){
            Position=0
        }
    }elseif(NextWeapon){ #Switch Pos Down
        soundPlay(0,1,SwitchSound)
        if(Position>0){
            Position--
        }elseif(Position==0){
            Position=3
        }
    }
}

#Pos 1 (Off)
if(Position==0){
    holoBodygroup(0,1,3)
    RGBF = vec(0)
    RGBR = vec(0)
    OnF = 0
    OnR = 0
    setName("Ditch Lights\n-Off-")
}

#Pos 2 (Front Only)
if(Position==1){
    holoBodygroup(0,1,0)
    RGBF = LightRGB
    RGBR = vec(0)
    OnF = 1
    OnR = 0
    setName(RockLights ? "Ditch Lights ("+UnlockKey+")\n-Top On-" : "Ditch Lights ("+UnlockKey+")\n-Front-")
}

#Pos 3 (Both)
if(Position==2){
    holoBodygroup(0,1,1)
    RGBF = LightRGB
    RGBR = LightRGB
    OnF = 1
    OnR = 1
    setName(RockLights ? "Ditch Lights ("+UnlockKey+")\n-Both On-" : "Ditch Lights ("+UnlockKey+")\n-Both-")
}

#Pos 4 (Rear Only)
if(Position==3){
    holoBodygroup(0,1,2)
    RGBF = vec(0)
    RGBR = LightRGB
    OnF = 0
    OnR = 1
    setName(RockLights ? "Ditch Lights ("+UnlockKey+")\n-Bottom On-" : "Ditch Lights ("+UnlockKey+")\n-Rear-")
}
